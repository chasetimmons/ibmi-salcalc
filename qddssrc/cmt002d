      *********************************************************************
      *   DSPF: CMT002D                                                   *
      *   DATE: 240307  BY: CTIMMONS                                      *
      *********************************************************************
     A                                      INDARA
     A                                      PRINT
     A                                      CA03(03 'F3=Exit')
     A                                      CA05(05 'F5=Reset')
     A          R CALC01
      *********************************************************************
      *   HEADER                                                          *
      *********************************************************************
     A            PGMREF        10      1  2
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 72TIME
     A                                  2 26'HANDY-DANDY PAYROLL CALCULATOR'
     A                                      COLOR(WHT)
     A                                  3  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
      *********************************************************************
      *   CONTENT                                                         *
      *********************************************************************
      *** 1
     A                                  5  5'Calculate one-time check for a sal-
     A                                      aried employee:                    -
     A                                           '
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                  6  6'Enter Semi-Monthly Rate . . :'
     A            D1_SEMIRT      7N 2B  6 36
     A            D1_HRLYRT     30   O  6 49COLOR(WHT)
     A                                  7  6'Enter Number of Days Worked :'
     A            D1_DAYSWK      3N 0B  7 36
     A            D1_HOURWK     30   O  7 49COLOR(WHT)
     A            D1_CHKAMT     30   O  9 49COLOR(WHT)
      *** 2
     A                                 11  5'Convert hourly rate to annual sala-
     A                                      ry:                                -
     A                                           '
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 12  6'Enter Hourly Rate . . . . . :'
     A            D2_HRLYRT      5N 2B 12 36
     A            D2_ANNLRT     30     12 49COLOR(WHT)
     A            D2_SEMIRT     30     13 49COLOR(WHT)
      *** 3 
     A                                 15  5'Convert annual salary to hourly ra-
     A                                      te:                                -
     A                                           '
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 16  6'Enter Annual Salary . . . . :'
     A            D3_ANNLRT      9N 2B 16 36
     A            D3_HRLYRT     30     16 49COLOR(WHT)
     A            D3_SEMIRT     30     17 49COLOR(WHT) 
      *** 4 
     A                                 19  5'Calculate percentage change:      -
     A                                                                         -
     A                                           '
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 20  6'Enter New Rate  . . . . . . :'
     A            D4_NRATE       7N 2B 20 36
     A                                 21  6'Enter Old Rate  . . . . . . :'
     A            D4_ORATE       7N 2B 21 36
     A            D4_PCTCHG     30     21 47COLOR(WHT)
      *********************************************************************
      *   FOOTER                                                          *
      *********************************************************************
     A                                 23  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 24  5'F3=Exit'
     A                                      COLOR(BLU)
     A                                 24 15'F5=Reset'
     A                                      COLOR(BLU)
